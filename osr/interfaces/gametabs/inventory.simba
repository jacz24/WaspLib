(*
Inventory
=========
Methods to interact with the inventory.

This extends SRL's TRSInventory and related methods.
*)

{$DEFINE WL_INVENTORY_INCLUDED}
{$IFNDEF WL_OSR}
  {$I WaspLib/osr.simba}
{$ENDIF}

const
  //Vertical 2 by 2 columns pattern. Normal and "reversed"
  DROP_PATTERN_TWO_ROW: TIntegerArray = [0,4,1,5,2,6,3,7,8,12,9,13,10,14,11,15,16,20,17,21,18,22,19,23,20,24,25,26,27,28];
  DROP_PATTERN_TWO_ROW_R: TIntegerArray = [0,4,1,5,2,6,3,7,11,15,10,14,9,13,8,12,16,20,17,21,18,22,19,23,20,24,28,27,26,25];


(*
TRSItem.Reorder
~~~~~~~~~~~~~~~
.. pascal:: function TRSItem.Reorder(LowToHi: Boolean = True): TRSItem;

Used to retrieve reordered items.
Items that have several "doses" or "portions" like a Saradomin brew (1..4) might need to be reordered sometimes.

When we want to consume them, we usually want to prioritize lower dosage items to get inventory space.
However when we are withdrawing it from the bank, we want to prioritize the highest dosage items to maximize trips length.

This function reorders depending on what you set in **LowToHi**. By default it reorders items from low to high.
*)
function TRSItem.Reorder(LowToHi: Boolean = True): TRSItem;
var
  str, first, last: String;
  i, j: Int32;
begin
  str := ToStr(Self);

  if not str.Contains('..') then
    Exit(Self);

  i := StrToInt(str.Before('..')[High(str.Before('..'))]);
  j := StrToInt(str.After('..')[Low(str.Before('..'))]);

  if (LowToHi and (i > j)) or (not LowToHi and (i < j)) then
  begin
    first := ToStr(j);
    last  := ToStr(i);
  end
  else
  begin
    first := ToStr(i);
    last  := ToStr(j);
  end;

  Result := str.Replace('(' + ToStr(i) + '..' + ToStr(j) + ')', '(' + first + '..' + last + ')');
end;

(*
TRSItem.GetPortions
~~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSItem.GetPortions(): Int32;

Get the amount of portions in the multi dose item (an item with "(x..z)").
*)
function TRSItem.GetPortions(): Int32;
var
  str: String;
  i, j: Int32;
begin
  str := ToStr(Self);

  if not str.Contains('..') then
    Exit(1);

  i := StrToInt(str.Before('..')[High(str.Before('..'))]);
  j := StrToInt(str.After('..')[Low(str.Before('..'))]);

  if i > j then
    Result := i
  else
    Result := j
end;

(*
TRSItem.GetPortion
~~~~~~~~~~~~~~~~~~
.. pascal:: function TRSItem.GetPortion(): Int32;

Get the portion number of one specific Item.

Example
-------
  Item := 'Saradomin brew(4)';

  WriteLn Item.GetPortion; //This will print 4.
*)
function TRSItem.GetPortion(): Int32;
var
  Str: String;
begin
  Str := ToStr(Self);

  if not Str.Contains('(') then
    Exit(1);

  Result := StrToInt(Str.Before(')')[High(Str.Before(')'))]);
end;

(*
Item.GetArray
~~~~~~~~~~~~~
.. pascal:: function TRSItem.GetArray(): TRSItemArray;

Used to retrieve an item array of our multi dose/portion item.

Example
-------
  var
    Item: TRSItem;
  begin
    Item := 'Saradomin brew(1..4)';

    WriteLn Item.GetArray();
    //This will print: ['Saradomin brew(1)', 'Saradomin brew(2)', 'Saradomin brew(3)', 'Saradomin brew(4)']
  end;
*)
function TRSItem.GetArray(): TRSItemArray;
var
  str: String;
  i, first, last: Int32;
begin
  str := ToStr(Self);

  if not str.Contains('..') then
    Exit([Self]);

  first := StrToInt(str.Before('..')[High(str.Before('..'))]);
  last :=  StrToInt(str.After('..')[Low(str.Before('..'))]);
  if first < last then
  begin
    for i := first to last do
      Result += str.Replace(ToStr(first) + '..' + ToStr(last), ToStr(i));
  end
  else if first > last then
  begin
    for i := first downto last do
      Result += str.Replace(ToStr(first) + '..' + ToStr(last), ToStr(i));
  end;
end;

(*
Item.GetSingle
~~~~~~~~~~~~~~
.. pascal:: function TRSItem.GetSingle(Lo: Boolean = True): TRSItem;

Used to retrieve the lowest or highest dose of our multi dose/portion item.

Example
-------
  var
    Item: TRSItem;
  begin
    Item := 'Saradomin brew(1..4)';

    WriteLn Item.GetSingle(True); //This will print: 'Saradomin brew(1)'
  end;
*)
function TRSItem.GetSingle(lo: Boolean = True): TRSItem;
var
  str: String;
  i, j: Int32;
begin
  str := ToStr(Self);

  if not str.Contains('..') then
    Exit(Self);

  i := StrToInt(str.Before('..')[High(str.Before('..'))]);
  j :=  StrToInt(str.After('..')[Low(str.Before('..'))]);

  if (lo and (i > j)) or (not lo and (i < j)) then
    Result := str.Replace(ToStr(i) + '..' + ToStr(j), ToStr(j))
  else
    Result := str.Replace(ToStr(i) + '..' + ToStr(j), ToStr(i));
end;


function TRSInventory.FindItems(items: TRSItemArray; out slots: TIntegerArray): Boolean; override;
var
  item: TRSItem;
  tmp: TRSItemArray;
begin
  if not Self.Open() then
    Exit;

  for item in items do
    tmp += item.GetArray();

  slots := Self.ItemInterface.IndicesOf(tmp);
  Result := Length(slots) > 0;
end;

function TRSInventory.FindItem(item: TRSItem; out slot: Int32): Boolean; override;
begin
  slot := -1;
  if not Self.Open() then
    Exit;
  slot := Self.ItemInterface.IndexOf(item.GetArray());
  Result := slot <> -1;
end;



function TRSInventory.ContainsItem(item: TRSItem): Boolean; override;
begin
  Result := Self.ContainsAny(item.GetArray());
end;

function TRSInventory.ContainsAny(items: TRSItemArray): Boolean; override;
var
  item: TRSItem;
  itemArray: TRSItemArray;
begin
  for item in Items do
    itemArray += item.GetArray();

  Result := inherited(itemArray);
end;

function TRSInventory.ContainsAll(items: TRSItemArray): Boolean; override;
var
  item: TRSItem;
  itemArray: TRSItemArray;
begin
  for item in items do
    itemArray += item.GetArray();

  Result := inherited(itemArray);
end;


function TRSInventory.ItemIsStackable(Item: TRSItem): Boolean;
begin
  Result := Self.CountItemStack(Item) > 0;
end;


function TRSInventory.FindAllItems(Items: TRSItemArray; out Slots: TIntegerArray): Boolean;
var
  Item: TRSItem;
begin
  for Item in Items do
  begin
    if not Self.FindItem(Item, Slots) then
      Exit(False);
    Slots := [];
  end;
  Result := True;
end;


function TRSInventory.SetSelectedItem(item: TRSItem): Boolean;
var
  slot: Int32;
begin
  if Inventory.FindItem(item, slot) then
    Result := Self.SetSelectedSlot(slot);
end;

function TRSInventory.Use(slot: Int32; item: TRSItem): Boolean; overload;
var
  slots: TIntegerArray;
  otherSlot: Int32;
begin
  Result := Self.IsSlotUsed(slot) and Self.FindItem(item, slots);
  if Result then
  begin
    otherSlot := Self.RandomSlotNearby(slot, slots);
    Result := Self.Use(slot, otherSlot);
  end;
end;


function TRSInventory.GetUsedSlots(): TIntegerArray;
begin
  Result := Self.ItemInterface.GetUsedSlots();
end;

function TRSInventory.GetEmptySlots(): TIntegerArray;
begin
  Result := Self.ItemInterface.GetEmptySlots();
end;


function TRSInventory.CountEmptySlots(): Int32;
begin
  Result := Self.ItemInterface.CountEmptySlots();
end;

function TRSInventory.HasSpace(slotsNeeded: Int32 = 1): Boolean;
begin
  Result := slotsNeeded <= Self.CountEmptySlots();
end;


function TRSInventory.FindRandomItems(items: TRSItemArray): TIntegerArray;
begin
  Result := Self.ItemInterface.FindRandomItems(items);
end;

function TRSInventory.ContainsRandomItems(items: TRSItemArray): Boolean;
begin
  Result := Self.ItemInterface.ContainsRandomItems(items);
end;

function TRSInventory.HasRandomItem(items: TRSItemArray): Boolean; deprecated 'Use TRSInventory.ContainsRandomItems() instead.';
begin
  Result := Self.ItemInterface.ContainsRandomItems(items);
end;


function TRSInventory.Drag(initialSlot, finalSlot: Int32): Boolean;
begin
  Result := Self.ItemInterface.Move(initialSlot, finalSlot);
end;

function TRSInventory.WaitItemFade(Slot: Int32): Boolean;
begin
  if WaitUntil(not Inventory.IsSlotUsed(Slot), 10, 300) then
    Result := WaitUntil(Inventory.IsSlotUsed(Slot), 50, 300);
end;


function TRSInventory.ClickSlot(slot: Int32; option: String = ''): Boolean; override;
var
  i: Int32;
  upText: String;
begin
  if Self.MouseSlot(slot) then
  begin
    upText := MainScreen.GetUpText();

    if not option.Contains('>') and upText.Contains('>') then
    begin
      ChooseOption.Select('Cancel');
      Exit;
    end;

    if (option = '') or upText.Contains(option) then
    begin
      Mouse.Click(MOUSE_LEFT);

      if (Random < Antiban.GetUniqueNumber(0.07, 0.05, 0.1)) then
      for i := 0 to Round(Antiban.GetUniqueNumber(2, 1, 3)) do
      begin
        Mouse.Click(Mouse.Position(), MOUSE_LEFT);

        Wait(0, 100, wdLeft);
      end;

      Exit(True);
    end;

    Result := ChooseOption.Select(option)
  end;
end;

function TRSInventory.ClickSlot(slot: Int32; options: TStringArray): Boolean; overload;
var
  i: Int32;
  upText: String;
begin
  if Self.MouseSlot(slot) then
  begin
    upText := MainScreen.GetUpText();

    if not options.Contains('>') and upText.Contains('>') then
    begin
      ChooseOption.Select('Cancel');
      Exit;
    end;

    if (options = []) or upText.ContainsAny(options) then
    begin
      Mouse.Click(MOUSE_LEFT);
      if (Random < Antiban.GetUniqueNumber(0.07, 0.05, 0.1)) then
      for i := 0 to Round(Antiban.GetUniqueNumber(2, 1, 3)) do
      begin
        Mouse.Click(Mouse.Position(), MOUSE_LEFT);

        Wait(0, 100, wdLeft);
      end;

      Exit(True);
    end;

    Result := ChooseOption.Select(options);
  end;
end;


function TRSInventory.ClickItem(item: TRSItem; option: String = ''): Boolean; override;
var
  i: Int32;
  upText: String;
begin
  if Self.MouseItem(item) then
  begin
    upText := MainScreen.GetUpText();

    if not option.Contains('>') and upText.Contains('>') then
    begin
      ChooseOption.Select('Cancel');
      Exit;
    end;

    if (option = '') or upText.Contains(option) then
    begin
      Mouse.Click(MOUSE_LEFT);

      if (Random < Antiban.GetUniqueNumber(0.07, 0.05, 0.1)) then
      for i := 0 to Round(Antiban.GetUniqueNumber(2, 1, 3)) do
      begin
        Mouse.Click(Mouse.Position(), MOUSE_LEFT);

        Wait(0, 100, wdLeft);
      end;

      Exit(True);
    end;

    Result := ChooseOption.Select(option)
  end;
end;

function TRSInventory.ClickItem(item: TRSItem; options: TStringArray): Boolean; overload;
var
  i: Int32;
  upText: String;
begin
  if Self.MouseItem(item) then
  begin
    upText := MainScreen.GetUpText();

    if not options.Contains('>') and upText.Contains('>') then
    begin
      ChooseOption.Select('Cancel');
      Exit;
    end;

    if (options = []) or upText.ContainsAny(options) then
    begin
      Mouse.Click(MOUSE_LEFT);
      if (Random < Antiban.GetUniqueNumber(0.07, 0.05, 0.1)) then
      for i := 0 to Round(Antiban.GetUniqueNumber(2, 1, 3)) do
      begin
        Mouse.Click(Mouse.Position(), MOUSE_LEFT);

        Wait(0, 100, wdLeft);
      end;

      Exit(True);
    end;

    Result := ChooseOption.Select(options);
  end
end;
